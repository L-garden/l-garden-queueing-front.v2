def APP_NAME
def APP_VERSION
def DOCKER_IMAGE_NAME
def NEED_DEPLOY = true

pipeline {
    agent any

    parameters {
        gitParameter branch: '',
                branchFilter: '.*',
                defaultValue: 'origin/main',
                description: '',
                listSize: '0',
                name: 'TAG',
                quickFilterEnabled: false,
                selectedValue: 'DEFAULT',
                sortMode: 'DESCENDING_SMART',
                tagFilter: '*',
                type: 'PT_BRANCH_TAG'
        booleanParam(defaultValue: true, description: '', name: 'COPY_DOCKER_COMPOSE')
        string(defaultValue: 'onprem', description: '', name: 'APP_PROFILE')
    }

    environment {
        GIT_URL = "https://github.com/l-garden/l-garden-queueing-front.v2.git"
        GITHUB_CREDENTIAL = "github-token"
        GITHUB_REGISTRY_TOKEN = credentials('GITHUB_REGISTRY_TOKEN')
        GITHUB_USERNAME = credentials('GITHUB_USERNAME')
        DOCKER_REGISTRY = "ghcr.io/l-garden"
        SSH_HOST = credentials('QUEUEING_FRONTEND_SSH_HOST')
        SSH_PORT = credentials('QUEUEING_FRONTEND_SSH_PORT')
        SSH_USER = credentials('QUEUEING_FRONTEND_SSH_USER')
        SSH_PASS = credentials('QUEUEING_FRONTEND_SSH_PASS')
    }

    options {
        disableConcurrentBuilds()
        buildDiscarder(logRotator(numToKeepStr: "30", artifactNumToKeepStr: "30"))
        timeout(time: 120, unit: 'MINUTES')
    }

    tools {
        nodejs "NodeJs 24.2.0"
        dockerTool "docker"
    }

    stages {
        stage('Set Name & Version') {
            steps {
                script {
                    APP_NAME = sh(
                            script: "npm pkg get name | sed 's/\"//g'",
                            returnStdout: true
                    ).trim()
                    APP_VERSION = sh(
                            script: "npm pkg get version | sed 's/\"//g'",
                            returnStdout: true
                    ).trim()
                    DOCKER_IMAGE_NAME = "${DOCKER_REGISTRY}/${APP_NAME}:${APP_VERSION}"
                }

                sh "echo DOCKER_IMAGE_NAME is ${DOCKER_IMAGE_NAME}"
            }
        }

        stage('Build Application') {
            steps {
                script {
                    if (params.APP_PROFILE == "onprem") {
                        sh "npm i"
                        sh "npm run build:onprem"
                    } else {
                        error("Profile을 다시 확인해주세요")
                    }
                }
            }
        }

        stage('Check Docker Image') {
            steps {
                script {
                    def imageExists = sh(
                            script: "docker manifest inspect ${DOCKER_IMAGE_NAME} > /dev/null 2>&1",
                            returnStatus: true
                    ) == 0

                    if (imageExists) {
                        echo "이미지 ${DOCKER_IMAGE_NAME}가 이미 레지스트리에 존재합니다. 이후 stage를 건너뜁니다."
                        NEED_DEPLOY = false
                        return
                    }
                }
            }
        }

        stage('Build image') {
            when {
                expression {NEED_DEPLOY == true}
            }
            steps {
                script {
                    docker.build(
                            "${DOCKER_IMAGE_NAME}",
                            "--file .docker/Dockerfile ."
                    )
                }
            }
        }

        stage('Push Docker Image') {
            when {
                expression {NEED_DEPLOY == true}
            }
            steps {
                script {
                    docker.withRegistry("https://ghcr.io", GITHUB_CREDENTIAL) {
                        docker.image("${DOCKER_IMAGE_NAME}").push()
                    }
                    sh "docker rmi ${DOCKER_IMAGE_NAME}"
                }
            }
        }

        stage('Copy docker-compose') {
            when {
                expression {NEED_DEPLOY == true}
            }
            steps {
                script {
                    if (params.COPY_DOCKER_COMPOSE == true) {
                        sh '''
                        sshpass -p $SSH_PASS scp -v -P $SSH_PORT -o StrictHostKeyChecking=no ./.docker/docker-compose-nginx.yaml $SSH_USER@$SSH_HOST:/home/queueing/frontend/docker-compose-nginx.yaml
                        '''
                    }
                }
            }
        }

        stage('Deploy') {
            when {
                expression {NEED_DEPLOY == true}
            }
            steps {
                script {
                    println(APP_VERSION)
                    sh """
                    sshpass -p $SSH_PASS ssh -p $SSH_PORT $SSH_USER@$SSH_HOST "echo ${env.GITHUB_REGISTRY_TOKEN} | docker login ghcr.io -u ${env.GITHUB_USERNAME} --password-stdin; \
                        TAG=${APP_VERSION} docker compose -f ./frontend/docker-compose-nginx.yaml up -d
                        "
                    """
                }
            }
        }
    }
}